# Maintainer: Ezekiel Bethel <stary@9net.org>

pkgname=switch-libpcre2
pkgver=10.32
pkgrel=1
pkgdesc='libpcre2 port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://www.pcre.org/'
license=('BSD')
options=(!strip libtool staticlibs)
depends=()
source=("https://ftp.pcre.org/pub/pcre/pcre2-${pkgver}.tar.gz")

sha256sums=('9ca9be72e1a04f22be308323caa8c06ebd0c51efe99ee11278186cafbc4fe3af')

build() {
  cd pcre2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  # Disable building programs & tests.
  # There is probably a better way to do this.
  sed -i 's/bin_PROGRAMS += .*//' Makefile.am
  sed -i 's/TESTS += .*//' Makefile.am
  sed -i 's/noinst_PROGRAMS += .*//' Makefile.am
	
  autoreconf
  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --enable-pcre2-16 --enable-pcre2-32

  make
}

package() {
  cd pcre2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
}
