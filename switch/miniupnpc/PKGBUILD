# Contributor: Ezekiel Bethel <zek@9net.org>

pkgname=switch-miniupnpc
pkgver=2.1.20190824
pkgrel=1
pkgdesc='UPNP support library (for Nintendo Switch homebrew development)'
arch=('any')
url="http://miniupnp.free.fr/"
license=('LGPL')
options=(!strip libtool staticlibs)
source=("http://miniupnp.free.fr/files/download.php?file=miniupnpc-${pkgver}.tar.gz" "miniupnpc.patch")

sha256sums=('1fadefea0ecdceb3e35bb5d901f0db880c826740e93148ddfb087b4b4389e165' 'b6b7e16cfd1e78069d352b22defed7e2e23924562da8ab17dd0988a13f0f22c5')
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')
build() {
  cd miniupnpc-$pkgver

  source /opt/devkitpro/switchvars.sh
  export CC=aarch64-none-elf-gcc
  export CXX=aarch64-none-elf-g++
  export LD=aarch64-none-elf-gcc
  export LDLIBS=$LIBS

  patch -p1 -i $srcdir/miniupnpc.patch

  export OVERRIDE_OS_NAME=Switch
  export OVERRIDE_OS_VERSION=1.0
  HAVE_IPV6=no make
}

package() {
  cd miniupnpc-$pkgver

  source /opt/devkitpro/switchvars.sh

  make PREFIX=/opt/devkitpro/portlibs/switch DESTDIR="$pkgdir" install
}
